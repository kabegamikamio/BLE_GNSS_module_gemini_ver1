#include <Arduino.h>
#include <BLEDevice.h>
#include <BLEServer.h>
#include <BLEUtils.h>
#include <BLE2902.h>
#include <HardwareSerial.h>
#include <TinyGPS++.h>
#include <EEPROM.h>

// --- UUIDs ---
#define SERVICE_UUID_LNS "00001819-0000-1000-8000-00805f9b34fb"                    // ä½ç½®æƒ…å ±ã‚’é€ã‚‹ã®ã«ä½¿ã†ã‚µãƒ¼ãƒ“ã‚¹
#define CHARACTERISTIC_UUID_LN_FEATURE "00002a6a-0000-1000-8000-00805f9b34fb"      // LNSãŒæŒã¤ãƒ•ã‚£ãƒ¼ãƒ«ãƒ‰ã®ç¨®é¡ã‚’è¡¨ã™ã‚­ãƒ£ãƒ©
#define CHARACTERISTIC_UUID_LOCATION_SPEED "00002a67-0000-1000-8000-00805f9b34fb"  // ä½ç½®æƒ…å ±ã¨é€Ÿåº¦(ã‚ã¨æ™‚åˆ»)ã®ã‚­ãƒ£ãƒ©
#define SERVICE_UUID_CONFIG "c48e6067-5295-48d3-8d5c-0395f61792b1"                 // ESP32ã‚„GNSSãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã®è¨­å®šã«ä½¿ã†ã‚µãƒ¼ãƒ“ã‚¹
#define CHARACTERISTIC_UUID_SMA_WINDOW "c48e6068-5295-48d3-8d5c-0395f61792b1"      // ç§»å‹•å¹³å‡åŒºé–“ã®è¨­å®šç”¨ã‚­ãƒ£ãƒ©
#define CHARACTERISTIC_UUID_GNSS_RATE "c48e6069-5295-48d3-8d5c-0395f61792b1"       // GNSSã®æ¸¬ä½é »åº¦è¨­å®šç”¨ã‚­ãƒ£ãƒ©

// --- EEPROM Settings ---
#define EEPROM_SIZE 16  // SMA(1) + Rate(1) + Baud(4) + reserved
#define ADDR_SMA_SIZE 0
#define ADDR_GNSS_RATE 1

// --- ã‚°ãƒ­ãƒ¼ãƒãƒ«è¨­å®šå¤‰æ•° ---
int smaWindowSize = 5;                    // å¹³æ»‘åŒ–ã®ç§»å‹•å¹³å‡åŒºé–“: åˆæœŸå€¤ã¯5
const uint32_t gpsBaud = 115200;          // GNSSã¨ã®é€šä¿¡ã§ä½¿ã†ãƒœãƒ¼ãƒ¬ãƒ¼ãƒˆ: åˆæœŸå€¤ã¯115200
int currentGnssRateHz = 10;               // GNSSã®æ¸¬ä½é »åº¦: åˆæœŸå€¤ã¯10Hz
unsigned long bleNotifyIntervalMs = 100;  // ä½ç½®æƒ…å ±ã®é€šçŸ¥é »åº¦(åˆæœŸå€¤ã¯ 1/10 s = 100ms)

// --- é€Ÿåº¦è¨ˆç®—ç”¨ã®ã‚°ãƒ­ãƒ¼ãƒãƒ«å¤‰æ•° ---
double lastLat = 0.0, lastLng = 0.0;
unsigned long lastSpeedCalcMillis = 0;
float smoothedSpeedKph = 0.0;
float *speedSamples = nullptr;
int currentSampleIndex = 0;
int filledSamples = 0;
const int MAX_SMA_WINDOW_SIZE = 50;

// --- BLE ---
BLECharacteristic *pLocationSpeedCharacteristic;
BLECharacteristic *pSmaWindowCharacteristic;
BLECharacteristic *pGnssRateCharacteristic;
bool deviceConnected = false;
unsigned long lastGpsCheck = 0;

// LNSã§é€ã‚‹ãƒ‘ã‚±ãƒƒãƒˆã®æ§‹é€ ä½“
#pragma pack(push, 1)
struct LocationAndSpeed_19Byte {
  uint16_t flags;
  uint16_t instantaneousSpeed;
  int32_t latitude;
  int32_t longitude;
  uint16_t year;
  uint8_t month;
  uint8_t day;
  uint8_t hour;
  uint8_t minute;
  uint8_t second;
};
#pragma pack(pop)

// --- GPS Settings ---
String nameBLE = "KawaiiMyGNSS";
#define RX_PIN 23
#define TX_PIN 22
TinyGPSPlus gps;
HardwareSerial gpsSerial(1);


// --- é–¢æ•°ãƒ—ãƒ­ãƒˆã‚¿ã‚¤ãƒ—å®£è¨€ ---
void setSmaWindowSize(int newSize);
void sendUb(byte *ubloxCommand, size_t len);
bool setGnssRate(int newRateHz);
void saveGnssConfig();

// --- BLE Callbacks ---
// æ¥ç¶š/åˆ‡æ–­æ™‚ã®ã‚³ãƒ¼ãƒ«ãƒãƒƒã‚¯
class MyServerCallbacks : public BLEServerCallbacks {
  void onConnect(BLEServer *pServer) {
    deviceConnected = true;
    Serial.println("\nâœ… Central device connected!");
  }
  void onDisconnect(BLEServer *pServer) {
    deviceConnected = false;
    Serial.println("âŒ Central device disconnected.");
    pServer->getAdvertising()->start();
  }
};

// ç§»å‹•å¹³å‡æ›´æ–°æ™‚ã®ã‚³ãƒ¼ãƒ«ãƒãƒƒã‚¯
class SmaConfigCallbacks : public BLECharacteristicCallbacks {
  void onWrite(BLECharacteristic *pCharacteristic) {
    String value = pCharacteristic->getValue();
    if (value.length() == 1) {
      int newSize = (int)value[0];
      Serial.printf("Received new SMA size via BLE (raw byte): %d\n", newSize);
      setSmaWindowSize(newSize);
      pCharacteristic->setValue(String(smaWindowSize).c_str());
    }
  }
};

// æ¸¬ä½é »åº¦æ›´æ–°æ™‚ã®ã‚³ãƒ¼ãƒ«ãƒãƒƒã‚¯
class GnssConfigCallbacks : public BLECharacteristicCallbacks {
  void onWrite(BLECharacteristic *pCharacteristic) {
    String value = pCharacteristic->getValue();
    uint32_t receivedValue = 0;

    if (value.length() > 0 && value.length() <= 4) {
      for (int i = 0; i < value.length(); i++) {
        receivedValue = (receivedValue << 8) | (uint8_t)value[i];
      }
    } else {
      Serial.println("Invalid data length received via BLE.");
      return;
    }

    if (pCharacteristic == pGnssRateCharacteristic) {
      if (setGnssRate((int)receivedValue)) {
        pGnssRateCharacteristic->setValue(String(currentGnssRateHz).c_str());
      }
    }
  }
};

// --- é–¢æ•°å®šç¾© ---

// ç§»å‹•å¹³å‡ã‚’ä½¿ã£ã¦é€Ÿåº¦ã‚’å¹³æ»‘åŒ–
void updateSmoothedSpeed(float rawSpeedKph) {
  // å¹³æ»‘åŒ–ãŒæœ‰åŠ¹ã«ãªã£ã¦ã„ã¦ã‚‚ç§»å‹•å¹³å‡åŒºé–“ãŒ0ã«è¨­å®šã•ã‚Œã¦ã—ã¾ã£ã¦ã„ã‚‹å ´åˆã¯ç”Ÿé€Ÿåº¦ã‚’è¿”ã™
  if (smaWindowSize == 0) {
    smoothedSpeedKph = rawSpeedKph;
  }

  if (speedSamples != nullptr && smaWindowSize > 0) {
    speedSamples[currentSampleIndex] = rawSpeedKph;
    currentSampleIndex = (currentSampleIndex + 1) % smaWindowSize;

    // ãƒãƒƒãƒ•ã‚¡æœªå……å¡«ã®ã¨ãã¯ã€å……å¡«ã•ã‚ŒãŸã¶ã‚“ã®é€Ÿåº¦ã ã‘ã§å¹³å‡ã‚’è¨ˆç®—
    if (filledSamples < smaWindowSize) filledSamples++;

    float sum = 0.0;
    for (int i = 0; i < filledSamples; i++) {
      sum += speedSamples[i];
    }
    smoothedSpeedKph = sum / smaWindowSize;
  }
}

// ç§»å‹•å¹³å‡ã®åŒºé–“ã®è¨­å®šé–¢æ•°
void setSmaWindowSize(int newSize) {
  if (newSize < 0) return;
  if (newSize > MAX_SMA_WINDOW_SIZE) newSize = MAX_SMA_WINDOW_SIZE;

  if (newSize == 0) {
    if (speedSamples != nullptr) {
      delete[] speedSamples;
      speedSamples = nullptr;
    }
    Serial.println("Speed smoothing DISABLED.");
  } else {
    if (speedSamples != nullptr) delete[] speedSamples;

    speedSamples = new float[newSize];

    for (int i = 0; i < newSize; i++) speedSamples[i] = 0.0;

    currentSampleIndex = 0;
    filledSamples = 0;
    smoothedSpeedKph = 0.0;

    Serial.printf("Speed smoothing ENABLED with window size: %d\n", newSize);
  }

  smaWindowSize = newSize;
  EEPROM.write(ADDR_SMA_SIZE, smaWindowSize);
  EEPROM.commit();
  Serial.println("Saved new SMA setting to EEPROM.");
}

// GNSSãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã«è¨­å®šã‚³ãƒãƒ³ãƒ‰ã‚’é€ã‚‹
void sendUb(byte *ubloxCommand, size_t len) {
  byte ck_a = 0, ck_b = 0;
  for (size_t i = 2; i < len - 2; i++) {
    ck_a = ck_a + ubloxCommand[i];
    ck_b = ck_b + ck_a;
  }
  ubloxCommand[len - 2] = ck_a;
  ubloxCommand[len - 1] = ck_b;

  Serial.print("Sending UBX Command: ");
  for (size_t i = 0; i < len; i++) {
    gpsSerial.write(ubloxCommand[i]);
    Serial.printf("%02X ", ubloxCommand[i]);
  }
  Serial.println();
}

// æ¸¬ä½é »åº¦ã‚’å¤‰ãˆã‚‹
bool setGnssRate(int newRateHz) {
  // æœ‰åŠ¹ãªæ¸¬ä½é »åº¦ã¯ 1 ~ 25Hz
  if (newRateHz < 1 || newRateHz > 25) {
    Serial.printf("Invalid rate: %d Hz. Must be between 1 and 25.\n", newRateHz);
    return false;
  }

  // æ–°ã—ã„æ¸¬ä½å‘¨æœŸã‚’è¨ˆç®—
  bleNotifyIntervalMs = 1000 / newRateHz;
  Serial.printf("Setting measurement rate to %d Hz (%d ms)...\n", newRateHz, bleNotifyIntervalMs);

  // æ¸¬å®šé »åº¦ã€å‘¨æœŸã‚’æ›´æ–°ã€EEPROMã«ã‚‚æ ¼ç´
  currentGnssRateHz = newRateHz;
  EEPROM.write(ADDR_GNSS_RATE, (uint8_t)currentGnssRateHz);
  EEPROM.commit();

  // UBXãƒ•ã‚©ãƒ¼ãƒãƒƒãƒˆã«å¾“ã£ã¦ãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã«å‘½ä»¤ã‚’é€ä¿¡
  byte ubxCfgRate[] = { 0xB5, 0x62, 0x06, 0x08, 0x06, 0x00, 0x00, 0x00, 0x01, 0x00, 0x01, 0x00, 0x00, 0x00 };
  ubxCfgRate[6] = (uint16_t)bleNotifyIntervalMs & 0xFF;
  ubxCfgRate[7] = ((uint16_t)bleNotifyIntervalMs >> 8) & 0xFF;
  sendUb(ubxCfgRate, sizeof(ubxCfgRate));
  delay(150);
  saveGnssConfig();

  Serial.printf("BLE notify interval updated to %lu ms.\n", bleNotifyIntervalMs);
  Serial.println("Saved new Rate setting to EEPROM.");

  return true;
}

// CFGå‘½ä»¤ã‚’é€ã‚‹
void saveGnssConfig() {
  Serial.println("Saving configuration to GNSS receiver's BBR/Flash...");
  byte ubxCfgCfg[] = {
    0xB5, 0x62, 0x06, 0x09, 0x0D, 0x00,  // Header
    0x00, 0x00, 0x00, 0x00,              // clearMask = 0
    0xFF, 0xFF,                          // saveMask = 0xFFFFï¼ˆå…¨é …ç›®ä¿å­˜ï¼‰
    0x00, 0x00,                          // loadMask = 0
    0x03, 0x00, 0x00,                    // deviceMask = 0 â†ã“ã“ãŒå•é¡Œ
    0x17, 0x00, 0x00                     // CRC placeholder
  };

  sendUb(ubxCfgCfg, sizeof(ubxCfgCfg));
  delay(100);
}

// --- setup() ---
void setup() {
  Serial.begin(115200);
  EEPROM.begin(EEPROM_SIZE);

  delay(250);

  // --- EEPROMã‹ã‚‰è¨­å®šã‚’èª­ã¿è¾¼ã¿ ---
  // ç§»å‹•å¹³å‡ã®è¨­å®š
  int storedSize = EEPROM.read(ADDR_SMA_SIZE);
  smaWindowSize = (storedSize < 0 || storedSize > MAX_SMA_WINDOW_SIZE) ? 7 : storedSize;
  Serial.printf("Loaded SMA size from EEPROM: %d\n", smaWindowSize);
  setSmaWindowSize(smaWindowSize);

  // æ¸¬ä½é »åº¦ã®è¨­å®š
  int storedRate = EEPROM.read(ADDR_GNSS_RATE);
  currentGnssRateHz = (storedRate < 1 || storedRate > 25) ? 10 : storedRate;
  Serial.printf("Loaded GNSS Rate from EEPROM: %d Hz\n", currentGnssRateHz);
  bleNotifyIntervalMs = 1000 / currentGnssRateHz;

  // GNSSãƒ¢ã‚¸ãƒ¥ãƒ¼ãƒ«ã¨ã®é€šä¿¡é–‹å§‹
  Serial.println("\nğŸš€ Starting BLE GNSS module v2...");
  gpsSerial.begin(gpsBaud, SERIAL_8N1, RX_PIN, TX_PIN);
  Serial.printf("ğŸ›°ï¸  GPS Serial started at %u baud on RX:%d, TX:%d.\n", gpsBaud, RX_PIN, TX_PIN);

  // BLEãƒ‡ãƒã‚¤ã‚¹ã®è¨­å®šã‚’é–‹å§‹
  BLEDevice::init(nameBLE.c_str());
  BLEServer *pServer = BLEDevice::createServer();
  pServer->setCallbacks(new MyServerCallbacks());

  // LNS Service
  BLEService *pLnsService = pServer->createService(SERVICE_UUID_LNS);
  BLECharacteristic *pLnFeatureCharacteristic = pLnsService->createCharacteristic(CHARACTERISTIC_UUID_LN_FEATURE, BLECharacteristic::PROPERTY_READ);
  const uint32_t ln_feature = 0b0000000001000101;
  pLnFeatureCharacteristic->setValue((uint8_t *)&ln_feature, 4);
  pLocationSpeedCharacteristic = pLnsService->createCharacteristic(CHARACTERISTIC_UUID_LOCATION_SPEED, BLECharacteristic::PROPERTY_NOTIFY);
  pLocationSpeedCharacteristic->addDescriptor(new BLE2902());
  pLnsService->start();

  // --- Config Service ---
  BLEService *pConfigService = pServer->createService(SERVICE_UUID_CONFIG);

  // ç§»å‹•åŒºé–“è¨­å®šã‚­ãƒ£ãƒ©ã®åˆæœŸåŒ–
  pSmaWindowCharacteristic = pConfigService->createCharacteristic(CHARACTERISTIC_UUID_SMA_WINDOW, BLECharacteristic::PROPERTY_READ | BLECharacteristic::PROPERTY_WRITE);
  pSmaWindowCharacteristic->setCallbacks(new SmaConfigCallbacks());
  pSmaWindowCharacteristic->setValue(String(smaWindowSize).c_str());

  // æ¸¬ä½é »åº¦è¨­å®šã‚­ãƒ£ãƒ©ã®åˆæœŸåŒ–
  pGnssRateCharacteristic = pConfigService->createCharacteristic(CHARACTERISTIC_UUID_GNSS_RATE, BLECharacteristic::PROPERTY_READ | BLECharacteristic::PROPERTY_WRITE);
  pGnssRateCharacteristic->setCallbacks(new GnssConfigCallbacks());
  pGnssRateCharacteristic->setValue(String(currentGnssRateHz).c_str());

  pConfigService->start();

  // ã‚¢ãƒ‰ãƒã‚¿ã‚¤ã‚¸ãƒ³ã‚°ã®è¨­å®š
  BLEAdvertising *pAdvertising = BLEDevice::getAdvertising();
  pAdvertising->addServiceUUID(SERVICE_UUID_LNS);
  pAdvertising->addServiceUUID(SERVICE_UUID_CONFIG);
  pAdvertising->setScanResponse(false);
  pAdvertising->setMinPreferred(0x06);
  pAdvertising->setMaxPreferred(0x0C);

  // BLEã‚¢ãƒ‰ãƒã‚¿ã‚¤ã‚¸ãƒ³ã‚°é–‹å§‹
  BLEDevice::startAdvertising();
  Serial.printf("ğŸ“¡ BLE advertising started as '%s'.\n", nameBLE.c_str());
}

// --- loop() ---
void loop() {
  // --- ã‚·ãƒªã‚¢ãƒ«é€šä¿¡ã‚’å—ã‘ãŸéš›ã®å‡¦ç† ---
  if (Serial.available() > 0) {
    String input = Serial.readStringUntil('\n');
    input.trim();
    if (input.endsWith("Hz")) {  // æ¸¬ä½é »åº¦è¨­å®š
      int newRate = input.substring(0, input.length() - 2).toInt();
      if (setGnssRate(newRate)) {
        pGnssRateCharacteristic->setValue(String(currentGnssRateHz).c_str());
      }
    } else {  // ç§»å‹•å¹³å‡åŒºé–“è¨­å®š
      int newSize = input.toInt();
      if (String(newSize) == input) {
        setSmaWindowSize(newSize);
        pSmaWindowCharacteristic->setValue(String(smaWindowSize).c_str());
      }
    }
  }

  // ä½ç½®æƒ…å ±ã‚’å—ä¿¡ã™ã‚‹ã¾ã§å¾…æ©Ÿ
  while (gpsSerial.available() > 0) {
    if (gps.encode(gpsSerial.read())) {
      // ã¾ãšã€ã©ã¡ã‚‰ã‹ã®ã‚½ãƒ¼ã‚¹ã‹ã‚‰ç”Ÿã®é€Ÿåº¦(km/h)ã‚’å–å¾—ã™ã‚‹
      float rawSpeedKph = 0.0;
      rawSpeedKph = gps.speed.kmph();
      // æ¬¡ã«ã€å¹³æ»‘åŒ–ãŒæœ‰åŠ¹ãªã‚‰ã€ç”Ÿã®é€Ÿåº¦ã‚’å…ƒã«å¹³æ»‘åŒ–ã‚’è¡Œã†
      updateSmoothedSpeed(rawSpeedKph);
    }
  }

  // è¨­å®šã•ã‚ŒãŸä½ç½®æƒ…å ±æ›´æ–°é–“éš”ä»¥ä¸ŠçµŒéã—ãŸã‚‰ä½ç½®æƒ…å ±ã®é€ä¿¡é–‹å§‹
  if (millis() - lastGpsCheck > bleNotifyIntervalMs) {
    lastGpsCheck = millis();

    if (gps.location.isValid() && gps.time.isValid() && deviceConnected) {
      // LNSãƒ‘ã‚±ãƒƒãƒˆã‚’ä½œæˆ
      LocationAndSpeed_19Byte loc_data;
      memset(&loc_data, 0, sizeof(loc_data));

      loc_data.flags = 0b0000000001000101;

      float speedKmph = 0.0;
      if (smaWindowSize > 0) {
        speedKmph = smoothedSpeedKph;
      } else {
        // å¹³æ»‘åŒ–ãŒç„¡åŠ¹ãªã¨ãã¯ã€ç”Ÿé€Ÿåº¦ã‚’ä½¿ç”¨
        speedKmph = gps.speed.kmph();
      }

      loc_data.instantaneousSpeed = (uint16_t)(speedKmph * 100.0);
      loc_data.latitude = (int32_t)(gps.location.lat() * 10000000);
      loc_data.longitude = (int32_t)(gps.location.lng() * 10000000);

      loc_data.year = gps.date.year();
      loc_data.month = gps.date.month();
      loc_data.day = gps.date.day();
      loc_data.hour = gps.time.hour();
      loc_data.minute = gps.time.minute();
      loc_data.second = gps.time.second();

      pLocationSpeedCharacteristic->setValue((uint8_t *)&loc_data, sizeof(loc_data));
      pLocationSpeedCharacteristic->notify();

      Serial.printf("0,80,%.02f\n", speedKmph);
    }
  }
}